buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.commercehub.gradle.plugin:gradle-avro-plugin:0.22.0'
    classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
  }
}

plugins {
  id 'java'
  id 'idea'
  id 'com.google.cloud.tools.jib' version '3.2.1'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
version = '0.0.1'

repositories {
  mavenCentral()
  

  maven {
    url 'https://packages.confluent.io/maven'
  }
}

apply plugin: 'com.commercehub.gradle.plugin.avro'
apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
  implementation 'org.apache.avro:avro:1.11.0'
  implementation 'org.slf4j:slf4j-simple:1.7.36'
  implementation 'io.confluent:kafka-streams-avro-serde:7.1.0'
  testImplementation 'junit:junit:4.13.2'
  testImplementation "org.testcontainers:kafka:1.16.3"
}

test {
  testLogging {
    outputs.upToDateWhen { false }
    showStandardStreams = true
    exceptionFormat = 'full'
  }
}

task run(type: JavaExec) {
  main = 'io.confluent.developer.TransformEvents'
  classpath = sourceSets.main.runtimeClasspath
  args = ['configuration/dev.properties']
}

jar {
  manifest {
    attributes(
        'Class-Path': configurations.compileClasspath.collect { it.getName() }.join(' '),
        'Main-Class': 'io.confluent.developer.TransformEvents'
    )
  }
}

shadowJar {
  archiveBaseName = "kafka-transforming-standalone"
  archiveClassifier = ''
}
