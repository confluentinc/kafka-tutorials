buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "com.google.protobuf:protobuf-gradle-plugin:0.9.1"
  }
}

plugins {
  id "java"
  id "application"
  id "idea"
  id "com.google.cloud.tools.jib" version "3.3.1"
  id "com.github.johnrengelman.shadow" version "6.1.0"
  id "com.google.protobuf" version "0.9.1"
  id "com.github.davidmc24.gradle.plugin.avro" version "1.5.0"
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"
version = "0.0.1"

repositories {
  mavenCentral()

  maven {
    url "https://packages.confluent.io/maven"
  }
}

apply plugin: "com.github.johnrengelman.shadow"

dependencies {
  implementation "org.apache.avro:avro:1.11.1"
  implementation "com.google.protobuf:protobuf-java:3.21.10"
  implementation "org.slf4j:slf4j-simple:2.0.5"
  implementation "org.apache.kafka:kafka-streams:3.3.1"
  implementation "io.confluent:kafka-streams-avro-serde:7.3.0"
  implementation "io.confluent:kafka-streams-protobuf-serde:7.3.0"
  implementation 'com.google.code.gson:gson:2.10'

  testImplementation "org.apache.kafka:kafka-streams-test-utils:3.3.1"
  testImplementation 'junit:junit:4.13.2'
  testImplementation 'org.assertj:assertj-core:3.23.1'
}

test {
  testLogging {
    outputs.upToDateWhen { false }
    showStandardStreams = true
    events "passed", "skipped", "failed"
    exceptionFormat "full"
  }
}

jar {
  manifest {
    attributes(
        "Class-Path": configurations.compileClasspath.collect { it.getName() }.join(" "),
        "Main-Class": "io.confluent.developer.SerializationTutorial"
    )
  }
}

shadowJar {
  archiveBaseName = "kstreams-serialization-standalone"
  archiveClassifier = ''
}

// Define the main class for the application
mainClassName = 'io.confluent.developer.serialization.SerializationTutorial'

protobuf {
  generatedFilesBaseDir = "$buildDir/generated-main-proto-java/"

  protoc {
    artifact = "com.google.protobuf:protoc:3.21.10"
  }

}

clean {
  delete protobuf.generatedFilesBaseDir
}

idea {
  module {
    sourceDirs += file("${buildDir}/generated-main-proto-java/")
  }
}
