buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "com.commercehub.gradle.plugin:gradle-avro-plugin:0.22.0"
    classpath "com.github.jengelman.gradle.plugins:shadow:6.1.0"
  }
}

plugins {
  id "java"
  id "application"
  id "com.google.cloud.tools.jib" version "3.2.0"
  id "idea"
  id "eclipse"
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"
version = "0.0.1"
mainClassName = "io.confluent.developer.RunningAverage"

repositories {
  mavenCentral()
  maven { url 'https://packages.confluent.io/maven/' }
}

apply plugin: "com.commercehub.gradle.plugin.avro"
apply plugin: "com.github.johnrengelman.shadow"

dependencies {
  implementation "org.apache.avro:avro:1.11.0"
  implementation "org.slf4j:slf4j-simple:1.7.36"
  implementation "org.apache.kafka:kafka-streams:3.1.0"
  implementation "io.confluent:kafka-streams-avro-serde:7.1.0"
  implementation "com.typesafe:config:1.4.2"

  testImplementation "org.apache.kafka:kafka-streams-test-utils:3.1.0"
  testImplementation "junit:junit:4.13.2"
  testImplementation 'org.hamcrest:hamcrest:2.2'

  testCompileOnly "org.projectlombok:lombok:1.18.24"
  testAnnotationProcessor "org.projectlombok:lombok:1.18.22"
}

test {
  testLogging {
    outputs.upToDateWhen { false }
    showStandardStreams = true
    exceptionFormat = "full"
  }
}

jar {
  manifest {
    attributes(
        "Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(" "),
        "Main-Class": mainClassName
    )
  }
}

shadowJar {
  archiveBaseName = "aggregating-average-standalone"
  archiveClassifier = ''
}

jib {
  container {
    mainClass = mainClassName
    jvmFlags = ["-Dconfig.file=/prod.properties"]
  }
  extraDirectories.paths = file("${rootDir.getPath()}/configuration")
}
