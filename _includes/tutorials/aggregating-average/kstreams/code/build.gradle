buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "com.commercehub.gradle.plugin:gradle-avro-plugin:0.15.1"
    classpath "com.github.jengelman.gradle.plugins:shadow:4.0.2"
  }
}

plugins {
  id "java"
  id "application"
  id "com.google.cloud.tools.jib" version "2.5.0"
  id "idea"
  id "eclipse"
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"
version = "0.0.1"
mainClassName = "io.confluent.developer.RunningAverage"

repositories {
  jcenter()
  maven { url 'https://packages.confluent.io/maven/' }
}

apply plugin: "com.commercehub.gradle.plugin.avro"
apply plugin: "com.github.johnrengelman.shadow"

dependencies {
  implementation "org.apache.avro:avro:1.8.2"
  implementation "org.slf4j:slf4j-simple:1.7.30"
  implementation "org.apache.kafka:kafka-streams:2.5.1"
  implementation "io.confluent:kafka-streams-avro-serde:5.5.1"
  implementation "com.typesafe:config:1.4.0"

  testImplementation "org.apache.kafka:kafka-streams-test-utils:5.3.1-ce"
  testImplementation "junit:junit:4.12"
  testImplementation 'org.hamcrest:hamcrest:2.2'

  testCompileOnly "org.projectlombok:lombok:1.18.12"
  testAnnotationProcessor "org.projectlombok:lombok:1.18.12"
}

test {
  testLogging {
    outputs.upToDateWhen { false }
    showStandardStreams = true
    exceptionFormat = "full"
  }
}

jar {
  manifest {
    attributes(
        "Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(" "),
        "Main-Class": mainClassName
    )
  }
}

shadowJar {
  archiveBaseName = "aggregating-average-standalone"
  archiveClassifier = ''
}

jib {
  container {
    mainClass = mainClassName
    jvmFlags = ["-Dconfig.file=/prod.properties"]
  }
  extraDirectories.paths = file("${rootDir.getPath()}/configuration")
}